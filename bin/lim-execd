#!/usr/bin/env perl

use common::sense;
use Getopt::Long ();
use Pod::Usage ();
use Log::Log4perl ();

use EV ();
use AnyEvent ();

use Lim ();
use Lim::RPC::Server ();
use Lim::Executor ();
use Lim::Plugins ();
use Lim::Manager ();

my $help = 0;
my $host = "127.0.0.1";
my $port = 5355;
my $key;
my $log4perl;
my $wsdl = '/usr/share/lim/wsdl';
my $prefix;

Getopt::Long::GetOptions(
	'help|?' => \$help,
	'host:s' => \$host,
	'port:i' => \$port,
    'key=s' => \$key,
    'log4perl:s' => \$log4perl,
    'wsdl:s' => \$wsdl,
    'prefix:s' => \$prefix
) or Pod::Usage::pod2usage(2);
Pod::Usage::pod2usage(1) if $help;

unless (-d $wsdl) {
    print STDERR 'Path to WSDL directory invalid or non existing: ', $wsdl, "\n";
    exit(1);
}

if (defined $log4perl and -f $log4perl) {
    Log::Log4perl->init($log4perl);
}
else {
    Log::Log4perl->init( \q(
    log4perl.logger                = DEBUG, Lim
    log4perl.appender.Lim          = Log::Log4perl::Appender::Screen
    log4perl.appender.Lim.stderr   = 0
    log4perl.appender.Lim.layout   = Log::Log4perl::Layout::PatternLayout
    log4perl.appender.Lim.layout.ConversionPattern = %d %F [%L] %p: %m%n
    ) );
}

if (defined $prefix) {
    Lim::Config->{prefix} = [];
    
    foreach (split(/,/o, $prefix)) {
        s/\/+$//o;
        push(@{Lim::Config->{prefix}}, $_);
    }
}

my $cv = AnyEvent->condvar;
my @watchers;

push(@watchers,
# TODO Implement config files
#    AnyEvent->signal(signal => "HUP", cb => sub {
#        if (defined($server)) {
#            $server->reload;
#        }
#    }),
    AnyEvent->signal(signal => "INT", cb => sub {
        $cv->send;
    }),
    AnyEvent->signal(signal => "QUIT", cb => sub {
        $cv->send;
    }),
    AnyEvent->signal(signal => "TERM", cb => sub {
        $cv->send;
    }),
);

my $server = Lim::RPC::Server->new(
    host => $host,
    port => $port,
    key => $key,
    wsdl => $wsdl
);
$server->serve(
    Lim->new(type => 'executor'),
    Lim::Executor->new(server => $server)
    );

if (defined $server) {
    push(@watchers, $server);
    $cv->recv;
}

@watchers = ();

Lim::Manager->deinstance;
Lim::Plugins->deinstance;

__END__

=head1 NAME

lim-execd - Lim Executor Daemon

=head1 SYNOPSIS

lim-execd [options]

=head1 OPTIONS

=over 8

=item B<--host <ip>>

Specify the host/IP to bind to (default 127.0.0.1)

=item B<--port <port>>

Specify the port to listen on (default 5355)

=item B<--key <key>>

Specify the SSL/TLS key to use for HTTPS

=item B<--wsdl <path>>

Specify the WSDL directory (default /usr/share/lim/wsdl)

=item B<--help>

Print a brief help message and exits.

=back

=head1 DESCRIPTION

B<This program> will read the given input file(s) and do something
useful with the contents thereof.

=cut

