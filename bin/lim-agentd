#!/usr/bin/env perl

use common::sense;
use Getopt::Long ();
use Pod::Usage ();
use Log::Log4perl ();
$Log::Log4perl::JOIN_MSG_ARRAY_CHAR = '';

use AnyEvent ();

use Lim ();
use Lim::RPC::Server ();
use Lim::RPC::TLS ();
use Lim::Agent ();
use Lim::Plugins ();

my $help = 0;
my $conf;
my $host = '0.0.0.0';
my $port = 5353;
my $key;
my $log4perl;
my $html = '/usr/share/lim/html';
my $prefix;
my $foreground = 0;

Getopt::Long::GetOptions(
    'help|?' => \$help,
    'conf:s' => \$conf,
    'key:s' => \$key,
    'log4perl:s' => \$log4perl,
    'prefix:s' => \$prefix,
    'foreground' => \$foreground
) or Pod::Usage::pod2usage(2);
Pod::Usage::pod2usage(1) if $help;

if (defined $log4perl and -f $log4perl) {
    Log::Log4perl->init($log4perl);
}
elsif ($foreground) {
    Log::Log4perl->init( \q(
    log4perl.logger                   = DEBUG, Screen
    log4perl.appender.Screen          = Log::Log4perl::Appender::Screen
    log4perl.appender.Screen.stderr   = 0
    log4perl.appender.Screen.layout   = Log::Log4perl::Layout::PatternLayout
    log4perl.appender.Screen.layout.ConversionPattern = %d %F [%L] %p: %m%n
    ) );
}
else {
    Log::Log4perl->init( \q(
    log4perl.logger                    = DEBUG, SYSLOG
    log4perl.appender.SYSLOG           = Log::Dispatch::Syslog
    log4perl.appender.SYSLOG.min_level = debug
    log4perl.appender.SYSLOG.ident     = lim-agentd
    log4perl.appender.SYSLOG.facility  = daemon
    log4perl.appender.SYSLOG.layout    = Log::Log4perl::Layout::SimpleLayout
    ) );
}

if (defined $conf) {
    unless (-r $conf and Lim::LoadConfig($conf)) {
        print STDERR 'Unable to read configuration file: ', $conf, "\n";
        exit(1);
    }
    Lim::Config->{agent}->{config_file} = $conf;
}
else {
    Lim::LoadConfig('/etc/lim/agent.yaml');
}

Lim::LoadConfigDirectory('/etc/lim/agent.d');
Lim::UpdateConfig;

if (defined $key) {
    Lim::Config->{ssl}->{ca_file} = $key;
    Lim::Config->{ssl}->{cert_file} = $key;
    Lim::Config->{ssl}->{key_file} = $key;
}

if (defined $prefix) {
    Lim::Config->{prefix} = [];
    
    foreach (split(/,/o, $prefix)) {
        s/\/+$//o;
        push(@{Lim::Config->{prefix}}, $_);
    }
}

# FORK & SAVE PID

my $cv = AnyEvent->condvar;
my @watchers;

push(@watchers,
    AnyEvent->signal(signal => "HUP", cb => sub {
    }),
    AnyEvent->signal(signal => "PIPE", cb => sub {
    }),
    AnyEvent->signal(signal => "INT", cb => sub {
        $cv->send;
    }),
    AnyEvent->signal(signal => "QUIT", cb => sub {
        $cv->send;
    }),
    AnyEvent->signal(signal => "TERM", cb => sub {
        $cv->send;
    }),
);

$html =~ s/\/*$//o;

Lim::RPC::TLS->set_instance(Lim::RPC::TLS->new(
    key => Lim::Config->{ssl}->{key_file}
));

my $server = Lim::RPC::Server->new(
    uri => (@ARGV == 1 ? $ARGV[0] : \@ARGV)
);
$server->serve(qw(Lim::Agent));
$server->serve(Lim::Plugins->instance->LoadedModules);

if (defined $server) {
    push(@watchers, $server);
    $cv->recv;
}

@watchers = ();

__END__

=head1 NAME

lim-agentd - Lim Agent Daemon

=head1 SYNOPSIS

lim-agentd [options] <uri>

=head1 OPTIONS

=over 8

=item B<--conf <file>>

Specify the configure file to use (default /etc/lim/agent.yaml).

=item B<--key <key>>

Specify the SSL/TLS key to use for HTTPS.

=item B<--log4perl <file>>

Specify a Log::Log4perl configure file (default output to stdout).

=item B<--help>

Print a brief help message and exits.

=back

=head1 DESCRIPTION

...

=cut

